varnishtest "Test querymodifier vmod for empty params"

server s1 {
	rxreq
	txresp -body "OK1"
	expect req.url == "/feed/?q"

	rxreq
	txresp -body "OK1"
	expect req.url == "/sitemap.xml"
} -start

varnish v1 -vcl+backend {
	import std;
	import querymodifier;

	sub vcl_recv {
		std.syslog(180, "querymodifier before: " + req.url);
		set req.url = querymodifier.modifyparams(url=req.url, params="q,id", exclude_params=false);
		std.syslog(180, "querymodifier after: " + req.url);
	}
} -start

client c1 {
	txreq -url "/feed/?q=&not=1"
	rxresp
	expect resp.status == 200

	# This one is cached.
	txreq -url "/feed/?not=1&another=2&q="
	rxresp
	expect resp.status == 200

	# This will just return the same URL since there are no parameters.
	txreq -url "/sitemap.xml"
	rxresp
	expect resp.status == 200
} -run

varnish v1 -expect n_object == 2
varnish v1 -expect cache_miss == 2
varnish v1 -expect cache_hit == 1
